import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter
import java.util.concurrent.TimeUnit

buildscript {
    repositories {
        jcenter()
        maven { url = "https://maven.minecraftforge.net/" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.1.0"
    }
}
plugins {
    id('scala')
    id("maven-publish")
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "com.matthewprenger.cursegradle"
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = getProperty("modVersion")
group = "com.yogpc.qp" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AdditionalEnchantedMiner-1.12.2"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileScala {
    sourceCompatibility = targetCompatibility = "1.8"
    compileScala.options.compilerArgs = ['-Xlint:all']
    scalaCompileOptions.additionalParameters = ["-feature"/*, "-target:jvm-1.8", "-Ybackend:GenBCode", "-Ydelambdafy:method"*/]
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replace "\${version}", project.version
    replace '@FINGERPRINT@', project.hasProperty('signSHA1') ? project.getProperty('signSHA1') : ""
}

repositories {
//    maven {
//        name 'curseforge'
//        url 'https://minecraft.curseforge.com/api/maven/'
//    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev/"
    }
    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net"
    }
    maven {
        name "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name = "ic2"
        url = "https://maven.ic2.player.to/"
    }
    maven {
        name = "Mekanism"
        url = "https://maven.thiakil.com/"
    }
    maven {
        name 'Crafttweaker'
        url 'https://maven.blamejared.com/'
    }
    maven {
        name 'CTM'
        url 'https://maven.tterrag.com/'
    }
    maven {
        url = "https://www.cursemaven.com"
    }
}

dependencies {
    deobfCompile 'net.industrial-craft:industrialcraft-2:2.8.221-ex112:dev'
    deobfCompile "cofh:RedstoneFlux:1.12-2.1.0.7:universal"
    deobfCompile "cofh:CoFHCore:1.12.2-4.6.3.27:universal"
    deobfCompile "mezz.jei:jei_1.12.2:4.16.1.302"
    deobfCompile "com.mod-buildcraft:buildcraft-main:7.99.24.7"
    compileOnly "CraftTweaker2:CraftTweaker2-API:4.1.20.605"
    compileOnly "CraftTweaker2:ZenScript:4.1.20.605"
    compileOnly "mekanism:Mekanism:1.12.2-9.4.4.330+0b21085"
    compileOnly "curse.maven:bedrockores-275083:2602707"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from sourceSets.main.output
    exclude("*.md")
    manifest {
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "Kotori316",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Kotori316",
                "Implementation-Timestamp": ZonedDateTime.now().format(DateTimeFormatter.ISO_INSTANT),
                "Commit"                  : getCommit("1.12")
        ])
    }
}

task signJar(type: SignJar, dependsOn: reobfJar) {

    // Skips if the keyStore property is missing.
    onlyIf {
        project.hasProperty('keyStore')
    }

    // findProperty allows us to reference the property without it existing.
    // Using project.propName would cause the script to fail validation if
    // the property did not exist.
    keyStore = project.hasProperty('keyStore') ? project.getProperty("keyStore") : ""
    alias = project.hasProperty('keyStoreAlias') ? project.getProperty('keyStoreAlias') : ""
    storePass = project.hasProperty('keyStorePass') ? project.getProperty('keyStorePass') : ""
    keyPass = project.hasProperty('keyStoreKeyPass') ? project.getProperty('keyStoreKeyPass') : ""
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}
// Runs this task automatically when build is ran.
build.dependsOn signJar

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

// Tell the artifact system about our extra jars
artifacts {
    archives srcJar, deobfJar
}

curseforge {
    apiKey = project.hasProperty("curseforge_additional-enchanted-miner_key") ? getProperty("curseforge_additional-enchanted-miner_key") : ""
    project {
        id = '282837'
        changelogType = 'markdown'
        changelog = new File("temp_changelog.md")
        releaseType = 'release'
        mainArtifact(jar) {
            displayName = 'v' + project.version
        }
        addArtifact srcJar
        addArtifact deobfJar
    }
    options {
        debug = false // defaults to false
        javaVersionAutoDetect = false
    }
}

static String getCommit(String branch) {
    ProcessBuilder builder = new ProcessBuilder("git", "rev-parse", branch)
    def process = builder.start()
    process.waitFor(1L, TimeUnit.SECONDS)
    def reader = new BufferedReader(new InputStreamReader(process.getInputStream()))
    def commit = reader.readLine()
    reader.close()
    return commit
}

publishing {
    repositories {
        maven {
            name = "AzureRepository"
            url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
            credentials {
                username = project.findProperty("azureUserName") ?: System.getenv("AZURE_USER_NAME") ?: ""
                password = project.findProperty("azureToken") ?: System.getenv("AZURE_TOKEN") ?: "TOKEN"
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = "com.kotori316"
            artifactId = "AdditionalEnchantedMiner".toLowerCase()
            artifact srcJar {
                classifier = 'sources'
                extension = 'jar'
            }
            artifact deobfJar {
                classifier = 'deobf'
                extension = 'jar'
            }
            artifact jar
        }
    }
}
