import java.nio.file.Files
import java.nio.file.Paths
import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter
import java.util.concurrent.TimeUnit
import java.util.stream.Collectors

plugins {
    // id("scala")
    id("idea")
    id("maven-publish")
    // https://maven.fabricmc.net/net/fabricmc/fabric-loom/
    id 'fabric-loom' version "0.12-SNAPSHOT"
    //noinspection SpellCheckingInspection
    id "com.github.breadmoirai.github-release" version "2.2.12"
    //noinspection SpellCheckingInspection
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

archivesBaseName = "AdditionalEnchantedMiner-1.19-fabric"
version = project.mod_version
if (isInCI()) {
    String postFix = System.getenv("GITHUB_RUN_NUMBER") ?: "SNAPSHOT"
    // In remote CI/CD environment
    version = (version + "." + postFix)

    println("Set version to $version because I'm in CI.")
    println("CURSEFORGE_KEY: ${System.getenv().containsKey('CURSEFORGE_KEY')}")
} else {
    version = (version + ".0")
}

group = "com.kotori316"

loom {
    runs {
        client {
            configName = "Client"
            runDir = Boolean.parseBoolean(System.getenv("FABRIC_SERVER")) ? "run-server" : "run"
            programArgs(List.of("--username", "Kotori"))
        }
        server {
            configName = "Server"
            runDir = "run-server"
        }
        data {
            client()
            configName = "Data"
            runDir = "run-server"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.datagen"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.datagen.strict-validation"

            ideConfigGenerated = true
            source sourceSets.test
        }
        gameTest {
            name "GameTest"
            server()
            vmArg "-ea"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.gametest"
            //noinspection SpellCheckingInspection
            vmArg "-Dfabric-api.gametest.report-file=${project.buildDir}/test-results/test/game_test.xml"
            runDir = "build/game_test"
            source sourceSets.test
        }
    }
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/generated/resources'
        }
    }
}

repositories {
    maven {
        name = "BuildCraft"
        url = "https://alexiil.uk/maven/"
        content {
            //noinspection SpellCheckingInspection
            it.includeGroup("alexiil.mc.lib")
            //noinspection SpellCheckingInspection
            it.includeGroup("alexiil.mc.mod")
        }
    }
    maven {
        name = "REI-Architectury"
        url = "https://maven.shedaniel.me/"
    }
    maven {
        name = "ModMenu"
        url = "https://maven.terraformersmc.com/releases/"
        content {
            //noinspection SpellCheckingInspection
            it.includeModule("com.terraformersmc", "modmenu")
        }
    }
    maven {
        name = "What The Hell Is That"
        url = "https://maven.bai.lol"
        content {
            it.includeVersion("mcp.mobius.waila", "wthit-api", "fabric-${project.wthit_version}")
            it.includeVersion("mcp.mobius.waila", "wthit", "fabric-${project.wthit_version}")
            it.includeVersion("lol.bai", "badpackets", "fabric-${project.badpackets_version}")
        }
    }
    maven {
        name = "ModMaven" // Here as fallback.
        url = "https://modmaven.dev/"
    }
    maven { url = "https://maven.kyrptonaught.dev" }
    maven {
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
        content {
            it.includeGroup("curse.maven")
        }
    }
    repositories.stream()
            .filter { repo -> repo instanceof MavenArtifactRepository && !repo.getUrl().getScheme().contains("file") }
            .forEach { repo ->
                repo.content {
                    //noinspection SpellCheckingInspection
                    excludeVersionByRegex("net_fabricmc_yarn.*", ".*", ".*")
                    excludeVersionByRegex(".*", ".*", ".*mapped.*")
                }
            }
}

//noinspection SpellCheckingInspection
dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    //noinspection SpellCheckingInspection
    modRuntimeOnly "net.fabricmc:fabric-language-scala:${project.fabric_scala_version}"

    // library
    // implementation "org.scala-lang:scala-library:2.13.4"
    implementation "com.google.code.findbugs:jsr305:3.0.1"
    modCompileOnly "alexiil.mc.lib:libblockattributes-all:${project.bc_attribute_version}"

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}")
    modImplementation("teamreborn:energy:${project.tech_energy_version}")
    modCompileOnly("RebornCore:RebornCore-1.18:${project.RebornCore_version}") {
        transitive = false
    }
    modCompileOnly("TechReborn:TechReborn-1.18:${project.TechReborn_version}") {
        transitive = false
    }
    modCompileOnly("mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}")
    modRuntimeOnly("mcp.mobius.waila:wthit:fabric-${project.wthit_version}")
    modRuntimeOnly(group: "lol.bai", name: "badpackets", version: "fabric-${project.badpackets_version}")

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        //noinspection SpellCheckingInspection
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("com.terraformersmc:modmenu:${project.mod_menu_version}") {
        transitive = false
    }

    // Runtime Checks
    // if (!System.getenv("RUN_GAME_TEST")) modRuntimeOnly("alexiil.mc.mod:simplepipes-all:${project.simple_pipe_version}")
    // modRuntimeOnly("curse.maven:carpet-349239:3725895")
    // modRuntimeOnly("curse.maven:mining-dimensions-fabric-442921:3566587")
    //noinspection SpellCheckingInspection
    // modRuntimeOnly("net.kyrptonaught:customportalapi:0.0.1-beta47-1.18")

    // Test Dependencies.
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2')
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.2')
    testRuntimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2')
    testImplementation(group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.6.2')
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

@SuppressWarnings("SpellCheckingInspection")
private static boolean isInCI() {
    return Boolean.parseBoolean(System.getenv("GITHUB_ACTIONS")) || Boolean.parseBoolean(System.getenv("CI")) ||
            (!System.getProperty("os.name").toLowerCase().startsWith("windows") && Files.exists(Paths.get("/", ".dockerenv")))
}
// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release.set(17)
}

sourceSets {
    main {
        java {
            srcDir 'src/main/scala'
        }
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
    toolchain {
        it.languageVersion = JavaLanguageVersion.of(17)
    }
}

static def BRANCH() { return "1.19-fabric" }

jar {
    from "LICENSE"
    def timeFormat = DateTimeFormatter.ISO_INSTANT
    manifest {
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "Kotori316",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "Kotori316",
                "Implementation-Timestamp": ZonedDateTime.now().format(timeFormat),
                "Commit"                  : getCommit(BRANCH())
        ])
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// PUBLISH
String joinedLog() {
    def commit = getCommit(BRANCH())
    return String.join("\n",
            "# Additional Enchanted Miner for ${project.minecraft_version} and Fabric.",
            "",
            isInCI() ? "RELEASED VIA Ci/CD. It may contain bugs." : "Manually built.",
            "",
            "## Commit",
            "",
            "[$commit](https://github.com/Kotori316/QuarryPlus/tree/$commit)",
            "",
            "## Requires",
            "",
            "* [Fabric API](https://www.curseforge.com/minecraft/mc-mods/fabric-api)",
            "* [Fabric Language Scala](https://www.curseforge.com/minecraft/mc-mods/fabric-language-scala)",
            "* [Cloth Config API (Fabric)](https://www.curseforge.com/minecraft/mc-mods/cloth-config)",
            "",
            "## Built with",
            "",
            "* Minecraft ${project.minecraft_version}",
            "* Mapping Official",
            "* Fabric API ${project.fabric_version}",
            "* Fabric Loader ${project.loader_version}",
            "* Lib Block Attributes ${project.bc_attribute_version}",
            "* TechReborn Energy ${project.tech_energy_version}",
            "* TechReborn ${project.TechReborn_version}",
            "* Cloth ${project.cloth_version}",
            "* Mod Menu ${project.mod_menu_version}",
            "* WTHIT ${project.wthit_version}",
    )
}

String curseChangelog() {
    String fromFile = file("changelog.md").exists() ? file("changelog.md").getText() : ""
    String base = joinedLog()
    String last = "See https://github.com/Kotori316/QuarryPlus/commits/${BRANCH()} to get all change log."
    return base + System.lineSeparator() * 2 +
            "# Changelog" + System.lineSeparator() + fromFile + System.lineSeparator() +
            last
}

curseforge {
    apiKey = project.hasProperty("curseforge_additional-enchanted-miner_key") ? getProperty("curseforge_additional-enchanted-miner_key") : (System.getenv("CURSEFORGE_KEY") ?: "")
    project {
        id = '282837'
        changelogType = 'markdown'
        addGameVersion 'Fabric'
        addGameVersion project.minecraft_version
        changelog = curseChangelog()
        releaseType = isInCI() ? 'alpha' : 'beta'
        mainArtifact(remapJar) {
            displayName = "v" + project.version + "-fabric" + " [${project.minecraft_version}]"
        }
        relations {
            requiredDependency 'fabric-language-scala'
            requiredDependency 'fabric-api'
            requiredDependency 'cloth-config'
            //noinspection SpellCheckingInspection
            // requiredDependency 'libblockattributes'
        }
    }
    options {
        curseGradleOptions.debug = false // defaults to false
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}

githubRelease {
    repo.set('QuarryPlus')
    token = project.findProperty("githubToken") ?: System.getenv("REPO_TOKEN") ?: ""
    targetCommitish.set(BRANCH())
    prerelease.set(project.version.toString().contains("SNAPSHOT"))

    body.set(joinedLog())
    releaseAssets = fileTree("build/libs")
}

test {
    useJUnitPlatform()
}

task removeTestWorld(type: Delete) {
    delete("${project.buildDir}/game_test/world")
    onlyIf {
        file("${project.buildDir}/game_test/world").exists() || !file("${project.buildDir}/test-results/test").exists()
    }
    doLast {
        mkdir("${project.buildDir}/test-results/test")
    }
}
runGameTest.dependsOn(removeTestWorld)

static String getCommit(String branch) {
    try {
        ProcessBuilder builder = new ProcessBuilder("git", "rev-parse", branch)
        def process = builder.start()
        process.waitFor(1L, TimeUnit.SECONDS)
        def reader = new BufferedReader(new InputStreamReader(process.getInputStream()))
        def commit = reader.readLine()
        reader.close()
        return commit ?: ""
    } catch (IOException ignored) {
        if (!isInCI())
            println("Caught $ignored")
        return ""
    }
}

task CheckCompileOption() {
    doLast {
        List<String> strings = new ArrayList<>()
        strings.add("archivesBaseName: $archivesBaseName")
        strings.add("group: $group")
        strings.add("BRANCH: ${BRANCH()}")
        strings.add("Is in CI: " + isInCI())
        strings.add("project.group: ${project.group}")
        strings.add("version: $version")
        strings.add("Display name: ${"v" + project.version + "-fabric" + " [${project.minecraft_version}]"}")
        strings.add("runs: ${loom.getRunConfigs().asMap.toMapString()}")
        strings.add("Absolute Client RunDir: ${project.projectDir.toPath().resolve(loom.getRunConfigs().getByName("client").runDir).toAbsolutePath()}")
        strings.add("Absolute Server RunDir: ${project.projectDir.toPath().resolve(loom.getRunConfigs().getByName("server").runDir).toAbsolutePath()}")
        strings.add("-" * 20)
        strings.add("Jar: ${jar.getArchiveFile().get()}")
        strings.add("Remap Jar ${remapJar.getArchiveFile().get()}")
        strings.add("Source Jar ${sourcesJar.getArchiveFile().get()}")
        strings.add("RemapSource Jar ${remapSourcesJar.getArchiveFile().get()}")
        strings.add(String.valueOf(fileTree("build/libs").getFiles()))
        strings.add("-" * 20)
        strings.add("Now: " + ZonedDateTime.now().format(DateTimeFormatter.ISO_INSTANT))
        strings.add("Now in Japan: " + ZonedDateTime.now().withZoneSameInstant(ZoneId.of("Asia/Tokyo")).format(DateTimeFormatter.ISO_LOCAL_DATE_TIME))
        strings.add("-" * 20)
        strings.add("Repositories")
        for (def r : project.getRepositories()) {
            strings.add("${r.name} $r ${r instanceof MavenArtifactRepository ? r.getUrl() : "Not maven " + r.getClass()}")
        }

        println(strings.stream().collect(Collectors.joining(System.lineSeparator())))
        println("*" * 20)
        println("ChangeLog")
        println(curseChangelog())
    }
}

task copyToDrive(type: Copy, dependsOn: "build") {
    def PATH_NAME = "drive_path"
    from(remapJar.getArchiveFile())
    into(file(java.util.Optional.ofNullable(System.getenv(PATH_NAME)).orElse(".")))
    onlyIf {
        System.getenv(PATH_NAME) != null &&
                Files.exists(Paths.get(System.getenv(PATH_NAME)))
    }
}

publishing {
    repositories {
        maven {
            name = "AzureRepository"
            url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
            credentials {
                username = project.findProperty("azureUserName") ?: System.getenv("AZURE_USER_NAME") ?: ""
                password = project.findProperty("azureToken") ?: System.getenv("AZURE_TOKEN") ?: "TOKEN"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = "AdditionalEnchantedMiner-Fabric".toLowerCase()
            if (isInCI()) {
                // Set as SNAPSHOT
                version = project.snapshot_version + "-SNAPSHOT"
            } else {
                version = project.mod_version
            }
            from components.java
            pom {
                name = archivesBaseName
                description = "QuarryPlus for Minecraft ${project.minecraft_version}, build with Fabric ${project.fabric_version}"
                url = 'https://github.com/Kotori316/QuarryPlus/commits/' + BRANCH()
                packaging = "jar"
                withXml {
                    List<String> requiredArtifacts = ["cloth", "fabric-language-scala"]
                    def pomNode = asNode() as Node
                    pomNode.dependencies.'*'.findAll() { Node node ->
                        def valueList = node.value() as NodeList
                        String artifactId = ((valueList.get(1) as Node).value() as NodeList).get(0)
                        if (requiredArtifacts.stream().noneMatch { artifactId.contains(it) })
                            node.parent().remove(node)
                    }
                }
            }
        }
    }
}
